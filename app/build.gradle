apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        // replace with the current version of the Android plugin
        classpath 'com.android.tools.build:gradle:2.1.0'
        // the latest version of the android-apt plugin
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

apt {
    arguments {
        resourcePackageName android.defaultConfig.applicationId
        //noinspection GroovyAssignabilityCheck
        androidManifestFile variant.outputs[0].processResources.manifestFile

        // If you're using Android NBS flavors you should use the following line instead of hard-coded packageName
        // resourcePackageName android.defaultConfig.packageName

        // You can set optional annotation processing options here, like these commented options:
        // logLevel 'INFO'
        // logFile '/var/log/aa.log'
    }
}

android {
    //noinspection GroovyAssignabilityCheck
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    //noinspection GroovyAssignabilityCheck
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "com.mobiapp.nicewallpapers"
        minSdkVersion 15
        //noinspection GroovyAssignabilityCheck
        targetSdkVersion project.ANDROID_BUILD_TARGET_SDK_VERSION
        versionCode 1
        versionName "1.0.1"
    }

    signingConfigs {
        release {
            def signFile = file("../local.properties")
            def signingProps = new Properties()
            signingProps.load(new FileInputStream(signFile))

            storeFile file("../keystore/release.jks")
            storePassword signingProps['storePassword']
            keyAlias signingProps['keyAlias']
            keyPassword signingProps['keyPassword']
        }
        debug {
            storeFile file("../keystore/debug.jks")
        }
    }

    buildTypes {
        //noinspection GroovyMissingReturnStatement
        release {
            minifyEnabled true
            debuggable = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            applicationVariants.all { variant ->
                appendVersionNameVersionCode(variant, defaultConfig)
            }
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.debug
        }
    }
    lintOptions {
        abortOnError false
        lintConfig file('lint.xml')
    }
}

@SuppressWarnings("GroovyAssignabilityCheck")
def appendVersionNameVersionCode(variant, defaultConfig) {
    variant.outputs.each { output ->
        if (variant.buildType.name == android.buildTypes.debug.name) {
            if (output.zipAlign) {
                def file = output.outputFile
                output.outputFile = new File(file.parentFile, variant.buildType.name + "_" + defaultConfig.applicationId + "_v" + defaultConfig.versionName + "." + defaultConfig.versionCode + ".apk")
            }
        } else if (variant.buildType.name == android.buildTypes.release.name) {
            if (output.zipAlign) {
                def file = output.outputFile
                output.outputFile = new File(file.parentFile, variant.buildType.name + "_" + defaultConfig.applicationId + "_v" + defaultConfig.versionName + "." + defaultConfig.versionCode + ".apk")
            }
        }
    }
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.google.code.gson:gson:2.4'
    apt "org.androidannotations:androidannotations:3.3.2"
    compile 'org.androidannotations:androidannotations-api:3.3.2'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    apt "org.projectlombok:lombok:1.16.4"
    provided 'org.projectlombok:lombok:1.16.4'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'com.github.satyan:sugar:1.4'
    compile 'com.squareup:otto:1.3.7'
    compile 'com.google.android.gms:play-services-ads:8.4.0'
    compile 'com.commit451:NativeStackBlur:1.0.2'
    compile 'com.kaopiz:kprogresshud:1.0.5'
    compile 'com.commit451:PhotoView:1.2.5'
    compile 'com.facebook.android:audience-network-sdk:4.13.0'
}
